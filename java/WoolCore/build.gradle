apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'eclipse-wtp'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'nl.rrd'
version = '1.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8
archivesBaseName = 'wool-core'

repositories {
    jcenter()
}

dependencies {
    // Use JUnit test framework
	compile "org.slf4j:slf4j-api:1.7.25"
	compile "com.fasterxml.jackson.core:jackson-annotations:2.9.7"
	compile "com.fasterxml.jackson.core:jackson-core:2.9.7"
	compile "com.fasterxml.jackson.core:jackson-databind:2.9.7"
    testImplementation 'junit:junit:4.12'
}

jar {
	manifest {
		attributes 'Main-Class': 'nl.rrd.wool.parser.WoolParser'
		attributes 'Class-Path': configurations.compile.collect({ it.name }).join(' ')
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

distributions {
	main {
		contents {
			from configurations.compile
			from jar
		}
	}
}

[distZip, distTar, installDist]*.dependsOn jar

javadoc {
	options {
		links 'https://docs.oracle.com/javase/10/docs/api/'
		addStringOption('Xdoclint:all,-html', '-quiet')
	}
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment ->
				signing.signPom(deployment)
			}
			
			repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			
			snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			
			pom.project {
				name 'WOOL Dialogue Framework'
				packaging 'jar'
				description 'WOOL is a simple, powerful dialogue framework for creating virtual agent conversations.'
				url 'https://www.council-of-coaches.eu/wool/'
				
				scm {
					connection 'scm:git:git://github.com/RoessinghResearch/wool.git'
					developerConnection 'scm:git:ssh://github.com:RoessinghResearch/wool.git'
					url 'https://github.com/RoessinghResearch/wool/tree/master'
				}
				
				licenses {
					license {
						name 'MIT License'
						url 'https://opensource.org/licenses/MIT'
					}
				}
				
				developers {
					developer {
						id 'dhofs'
						name 'Dennis Hofs'
						email 'd.hofs@rrd.nl'
					}
				}
			}
		}
	}
}
